/*
 This is an EBNF description of the BS language.
 Notation: https://www.w3.org/TR/xml/#sec-notation
 Evaluate: https://bottlecaps.de/rr/ui
*/
program ::= (space* statement? eol)*

statement ::=
    (name space* "=" space* expr) |
    function_call |
    ("if" space+ expr) |
    ("elsif" space+ expr) |
    "else" |
    ("while" space+ expr) |
    ("for" name space* "=" space* number space+ "to" space+ number (space+ "step" space+ number)?) |
    "end"

function_call ::= name space* "(" expr (space* "," space* expr)* ")"

expr ::= ("(" space*)? (
    literal |
    name |
    function_call |
    (unop space* expr) |
    (expr space* binop space* expr)
    ) (space* ")")?

name ::= (alpha | "_") (alpha | digit | "_")*

literal ::= number | string | "true" | "false"
number ::= "-"? digit+
string ::= "'" ((all_characters - "'") | "\'" | "\n" | "\r" | "\t" )* "'"

binop ::= "+" | "-" | "*" | "/" | "%" |
          "&" | "|" | "^" | "<<" | ">>" |
          "<" | "<=" | ">" | ">=" | "==" | "!="
unop ::= "-" | "~"

alpha ::= [a-zA-Z]
digit ::= [0-9]

space ::= #x9 | #xD | #x20
eol ::= ("#" all_characters*)? #xA
all_characters ::= [#x20-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF]
